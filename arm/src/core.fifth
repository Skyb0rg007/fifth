\ vim: ft=forth


: QUIT
    ( empty return stack )
    FIFTH_INPUT_SOURCE 0 !
    POSTPONE [
    REFILL
    WHILE
        ['] INTERPRET CATCH
        CASE
            0  OF STATE @ 0= IF ." ok" THEN CR ENDOF
            -1 OF ( aborted ) ENDOF
            -2 OF ( abort" msg ) ENDOF
            ( default ) DUP ." Exception # " .
        ENDCASE
    REPEAT BYE ;

: ACTION-OF
    STATE @ IF
        POSTPONE ['] POSTPONE DEFER@
    ELSE
        ' DEFER@
    THEN ; IMMEDIATE

: BUFFER: ( u "name" -- ) ( -- addr )
    CREATE ALLOT ;


\ Defer words

: DEFER ( "name" -- )
    CREATE
        ['] ABORT ,
    DOES>
        @ EXECUTE ;

: DEFER! ( xt2 xt1 -- )
    >BODY ! ;

: DEFER@ ( xt1 -- xt2 )
    >BODY @ ;

: IS
    STATE @ IF
        POSTPONE ['] POSTPONE DEFER!
    ELSE
        ' DEFER!
    THEN ; IMMEDIATE


: isspace? ( c -- f )
    BL 1+ U< ;
: isnotspace? ( c -- f )
    isspace? 0= ;
: xt-skip ( addr1 n1 xt -- addr2 n2 )
    >R
    BEGIN
        DUP
    WHILE
        OVER C@ R@ EXECUTE
    WHILE
        1 /STRING
    REPEAT
    THEN
    R> DROP ;

: PARSE-NAME ( "name" -- c-addr u )
    SOURCE >IN @ /STRING
    ['] isspace? xt-skip OVER >R
    ['] isnotspace? xt-skip 
    2DUP 1 MIN + SOURCE DROP - >IN
    DROP R> TUCK - ;
